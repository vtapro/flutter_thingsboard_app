allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url ("https://jitpack.io/") }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin("com.android.application") ||
                project.plugins.hasPlugin("com.android.library")) {
            project.android {
                compileSdkVersion 35
                buildToolsVersion "35.0.0"
                // Force modern Java toolchain for all subprojects to avoid
                // "source/target value 8 is obsolete" warnings from plugins.
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
                // Suppress noisy Java compiler warnings from third-party plugins
                // that still target source/target 8 or use deprecated/unchecked APIs.
                // These do not affect runtime behavior of the app.
                tasks.withType(JavaCompile).configureEach {
                    // Reduce javac noise from transitive plugins
                    options.warnings = false
                    options.deprecation = false
                    options.compilerArgs += [
                            '-Xlint:-options',
                            '-Xlint:-deprecation',
                            '-Xlint:-unchecked'
                    ]
                }
                if (namespace == null) {
                        namespace project.group
                    }
            }
        }
    }
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
